## 1. Auditar Log Analytics Workspace

```kusto
// Ver tablas con datos recientes
search *
| summarize count() by $table
| sort by count_ desc
```



## 2. Auditar Agente - AMA

```kusto
// Consultar máquinas Arc con AMA instalada (Windows o Linux)
Resources
| where type == "microsoft.hybridcompute/machines/extensions"
| where name in~ ("AzureMonitorWindowsAgent", "AzureMonitorLinuxAgent")
| extend machineName = split(id, "/")[8]
| project machineName, osType = iif(name == "AzureMonitorWindowsAgent", "Windows", "Linux"),
          provisioningState = properties.provisioningState,
          version = properties.typeHandlerVersion,
          autoUpgrade = properties.autoUpgradeMinorVersion,
          resourceGroup, subscriptionId
```

```kusto
// Ver solo las máquinas donde AMA está correctamente instalado
Resources
| where type == "microsoft.hybridcompute/machines/extensions"
| where name in~ ("AzureMonitorWindowsAgent", "AzureMonitorLinuxAgent")
| where tostring(properties.provisioningState) == "Succeeded"
| extend machineName = split(id, "/")[8]
| project machineName, osType = iif(name == "AzureMonitorWindowsAgent", "Windows", "Linux"),
          version = properties.typeHandlerVersion,
          resourceGroup
```

```kusto
// Contar cuántas máquinas tienen AMA por tipo de SO
Resources
| where type == "microsoft.hybridcompute/machines/extensions"
| where name in~ ("AzureMonitorWindowsAgent", "AzureMonitorLinuxAgent")
| summarize count() by OS = iif(name == "AzureMonitorWindowsAgent", "Windows", "Linux")
```
## 2. Administrar VM ARC **


```kusto
// Servidores principales agregados segun el rango de tiempo
let trend = (Heartbeat
    | extend RGName = tolower(split(_ResourceId, "/")[4])
    | where RGName in ('rg-arc')
    | make-series InternalTrend=iff(count() > 0, 1, 0) default = 0 on TimeGenerated from ago(3d) to now() step 15m by _ResourceId
    | extend Trend=array_slice(InternalTrend, array_length(InternalTrend) - 30, array_length(InternalTrend) - 1)); 
let PerfCPU = (InsightsMetrics
    | where Origin == "vm.azm.ms"
    | extend RGName = tolower(split(_ResourceId, "/")[4])
    | where RGName in ('rg-arc')
    | where Namespace == "Processor" and Name == "UtilizationPercentage"
    | summarize AvgCPU=round(avg(Val), 2), MaxCPU=round(max(Val), 2) by _ResourceId
    | extend StatusCPU = case (
                             AvgCPU > 80,
                             2,
                             AvgCPU > 50,
                             1,
                             AvgCPU <= 50,
                             0,
                             -1
                         )
    );
let PerfMemory = (InsightsMetrics
    | where Origin == "vm.azm.ms"
    | extend RGName = tolower(split(_ResourceId, "/")[4])
    | where RGName in ('rg-arc')
    | where Namespace == "Memory" and Name == "AvailableMB"
    | summarize AvgMEM=round(avg(Val), 2), MaxMEM=round(max(Val), 2) by _ResourceId
    | extend StatusMEM = case (
                             AvgMEM > 4,
                             0,
                             AvgMEM >= 1,
                             1,
                             AvgMEM < 1,
                             2,
                             -1
                         )
    );
let PerfDisk = (InsightsMetrics
    | where Origin == "vm.azm.ms"
    | extend RGName = tolower(split(_ResourceId, "/")[4])
    | where RGName in ('rg-arc')
    | where Namespace == "LogicalDisk" and Name == "FreeSpaceMB"
    | extend Disk=tostring(todynamic(Tags)["vm.azm.ms/mountId"])
    | where (Disk =~ "C:" or Disk == "/")
    | summarize
        AvgDisk=round(avg(Val), 2),
        (TimeGenerated, LastDisk)=arg_max(TimeGenerated, round(Val, 2))
        by _ResourceId
    | extend StatusDisk = case (
                              AvgDisk < 5000,
                              2,
                              AvgDisk < 30000,
                              1,
                              AvgDisk >= 30000,
                              0,
                              -1
                          )
    | project _ResourceId, AvgDisk, LastDisk, StatusDisk
    );
PerfCPU
| join (PerfMemory) on _ResourceId
| join (PerfDisk) on _ResourceId
| join (trend) on _ResourceId
| project
    _ResourceId,
    StatusCPU,
    AvgCPU,
    MaxCPU,
    StatusMEM,
    AvgMEM,
    MaxMEM,
    StatusDisk,
    AvgDisk,
    LastDisk,
    ["Heartbeat Trend"] = Trend
| sort by StatusCPU, StatusDisk desc
```

```kusto
// Tiempo del procesador
let TopComputers = InsightsMetrics 
    | where Origin == "vm.azm.ms"
    | extend RGName = tolower(split(_ResourceId, "/")[4])
    | where RGName in ('rg-arc')
    | where _ResourceId contains "All" or "All" == "All"
    | where Namespace == "Processor" and Name == "UtilizationPercentage"
    | summarize AvgCPU = avg(Val) by Computer 
    | top 10 by AvgCPU desc
    | project Computer; 
InsightsMetrics 
| where Origin == "vm.azm.ms"
| where Computer in (TopComputers) 
| where Namespace == "Processor" and Name == "UtilizationPercentage"
| summarize Used_CPU = round(avg(Val), 1) by Computer, bin(TimeGenerated, (now() - ago(1d)) / 100)
| render timechart
```

```kusto
// Umbrales
let trend = 
    InsightsMetrics
    | where Origin == "vm.azm.ms"          
    | extend RGName = tolower(split(_ResourceId, "/")[4])
    | where _ResourceId contains "All" or "All" == "All"
    | where RGName in ('rg-arc')
    | where Namespace == "Processor" and Name == "UtilizationPercentage"
    | make-series Average = round(avg(Val), 3) default = 0 on TimeGenerated from ago(1d) to now() step totimespan('00:30:00') by _ResourceId     
    | project _ResourceId, ['Trend'] = Average; 
InsightsMetrics
| where Origin == "vm.azm.ms"
| extend RGName = tolower(split(_ResourceId, "/")[4])
| where RGName in ('rg-arc')
| where _ResourceId contains "All" or "All" == "All"
| where Namespace == "Processor" and Name == "UtilizationPercentage"
| summarize Average=round(avg(Val), 3) by _ResourceId
| join (trend) on _ResourceId
| extend Status = case (
                      Average > 80,
                      "Critical",
                      Average > 50,
                      "Warning",
                      Average <= 50,
                      "Healthy",
                      "Unknown"
                  )
| project Status, _ResourceId, Average, Trend
| sort by Status desc
```

```kusto
// MB Disponibles
let TopComputers = InsightsMetrics 
| where Origin == "vm.azm.ms"
| extend RGName = tolower(split(_ResourceId, "/")[4])
| where RGName in ('rg-arc')
| where _ResourceId contains "All" or "All"=="All"
| where Namespace == "Memory" and Name == "AvailableMB"
| summarize AvailableGBytes = round(avg(Val)/1024,2) by Computer
| top 10 by AvailableGBytes asc
| project Computer; 
InsightsMetrics 
| where Origin == "vm.azm.ms"
| where Computer in (TopComputers) 
| where Namespace == "Memory" and Name == "AvailableMB"
| summarize AvailableGBytes = round(avg(Val)/1024,2) by Computer, bin(TimeGenerated, (now() - ago(1d))/100)
| render timechart
```

```kusto
// Umbrales <4GB; Critico < 1Gb) Todos los VM
let trend = 
    InsightsMetrics
    | where Origin == "vm.azm.ms"           
    | extend RGName = tolower(split(_ResourceId, "/")[4])
    | where RGName in ('rg-arc')
    | where _ResourceId contains "All" or "All" == "All"
    | where Namespace == "Memory" and Name == "AvailableMB"
    | make-series Average = round(avg(Val), 3) default = 0 on TimeGenerated from ago(1d) to now() step totimespan('00:30:00') by _ResourceId     
    | project _ResourceId, ['Trend'] = Average; 
InsightsMetrics
| where Origin == "vm.azm.ms"
| extend RGName = tolower(split(_ResourceId, "/")[4])
| where RGName in ('rg-arc')
| where _ResourceId contains "All" or "All" == "All"
| where Namespace == "Memory" and Name == "AvailableMB"
| summarize ["Available GBytes"]=round(avg(Val) / 1024, 2) by _ResourceId
| join (trend) on _ResourceId
| extend Status = case (
                      ["Available GBytes"] > 4,
                      "Healthy",
                      ["Available GBytes"] >= 1,
                      "Warning",
                      ["Available GBytes"] < 1,
                      "Critical",
                      "Unknown"
                  )
| project Status, _ResourceId, ["Available GBytes"], Trend
| sort by ["Available GBytes"] asc
```

```kusto
// Umbrales (Advertencia > 60; Critico < 90) - Todas las VM
let trend = 
InsightsMetrics   
| where Origin == "vm.azm.ms"        
| extend RGName = tolower(split(_ResourceId, "/")[4])
| where RGName in ('rg-arc')
| where _ResourceId contains "All" or "All"=="All"
| where Namespace == "Memory" and Name == "AvailableMB"
| extend TotalMemory = toreal(todynamic(Tags)["vm.azm.ms/memorySizeMB"]) 
| extend CommittedMemoryPercentage = 100-((toreal(Val) / TotalMemory) * 100.0)
| make-series Average = round(avg(CommittedMemoryPercentage), 3) default = 0 on TimeGenerated from ago(1d) to now() step totimespan('00:30:00') by _ResourceId     
| project _ResourceId, ['Trend'] = Average; 
InsightsMetrics
| where Origin == "vm.azm.ms"
| extend RGName = tolower(split(_ResourceId, "/")[4])
| where RGName in ('rg-arc')
| where _ResourceId contains "All" or "All"=="All"
| where Namespace == "Memory" and Name == "AvailableMB"
| extend TotalMemory = toreal(todynamic(Tags)["vm.azm.ms/memorySizeMB"]) 
| extend CommittedMemoryPercentage = 100-((toreal(Val) / TotalMemory) * 100.0)
| summarize Average=round(avg(CommittedMemoryPercentage),3) by _ResourceId
| join (trend) on _ResourceId
| extend Status = case (
                  Average > 90, "Critical",
                  Average > 60, "Warning",
                  Average <= 60, "Healthy", "Unknown"
)
| project Status, _ResourceId, Average, Trend
| sort by Average 
```

```kusto
// Umbrales (Advertencia < 30GB; Critico < 5) - Todas las VM
let trend = InsightsMetrics 
| where Origin == "vm.azm.ms"
| extend RGName = tolower(split(_ResourceId, "/")[4])
| where RGName in ('rg-arc')         
| where _ResourceId contains "All" or "All"=="All"
| where Namespace == "LogicalDisk" and Name == "FreeSpaceMB"
| extend Disk=tostring(todynamic(Tags)["vm.azm.ms/mountId"])
| where (strlen(Disk) ==2 and Disk contains ":") or Disk=="/"
| extend Disco = strcat(Disk, " - ", _ResourceId)
| make-series Average = round(avg(Val), 3) default = 0 on TimeGenerated from ago(1d) to now() step totimespan('00:30:00') by Disco     
| project Disco, ['Trend'] = Average; InsightsMetrics
| where Origin == "vm.azm.ms"
| extend RGName = tolower(split(_ResourceId, "/")[4])
| where RGName in ('rg-arc')
| where _ResourceId contains "All" or "All"=="All"
| where Namespace == "LogicalDisk" and Name == "FreeSpaceMB"
| extend Disk=tostring(todynamic(Tags)["vm.azm.ms/mountId"])
| where (strlen(Disk) ==2 and Disk contains ":") or Disk=="/"
| extend Disco = strcat(Disk, " - ",_ResourceId )
| summarize Average=round(avg(Val),2) by Disco,_ResourceId,Disk
| join (trend) on Disco
| extend Status = case (
                  Average < 5000, "Critical",
                  Average < 30000, "Warning",
                  Average >= 30000, "Healthy", "Unknown"
)
| project Status, _ResourceId,Disk, Average, Trend
| sort by Average asc 
```